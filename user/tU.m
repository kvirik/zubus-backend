tU	;Библиотека функций для рейсов;27.01.11;v.1.7
	;_v.1.7_27.01.11_буквенные номера рейсов ж.д.
	;_v.1.6_14.07.10_добавлена DB
	;_v.1.5_29.07.09_универсально для Днепра и Харькова
	;_v.1.4_23.12.08_добавлена $$OurKas, $$SysKas
	;_v.1.3_23.04.08_для отказа от tSPVR, добавлены параметры 6 и 7 в $$VR
	;_v.1.2_22.02.08_перенесен tUTL (tUTL - больше нет !)
	;_v.1.1_01.10.07_исправления по поводу дообилечивания (см. $$nVR^tU(VR))
	Q
	;
	;---проверка эталона (возвращает 1, если есть)
ELRE(VR,DB)
	N D
	set DB=$g(DB)
	S D="" F  S D=$O(^|DB|tEL(D)) Q:D=""  I $D(^|DB|tEL(D,VR)) Q
	Q $T
	;
	;---проверка ходячести без ^tEL (возвращает 1, если есть)
MSRE(VR,DB)
	N D
	set DB=$g(DB)
	S D=+$h F  S D=$O(^|DB|tMS(D)) Q:D=""  I $D(^|DB|tMS(D,VR)) Q
	Q $T
	;---заголовок из VR
VR(VR,p,raz,DB)
	; VR -полный номер рейса
	; raz-символ-разделитель
	; p  -параметр воврата строки
	;     1-без форматирования                     {9:30■20■St1-St2■R}
	;     2-форм.ст. по левому краю               { 9:30■   20■[St1  ]-[St2  ]■R}
	;   * 3-форм.ст. по центру (умолчание)        { 9:30■   20■[  St1]-[St2  ]■R}
	;     4-форм.ст. по лев. краю с разделителем  { 9:30■   20■ [St1  ]- [St2  ]■R}
	;       (с дополнительным пробелом слева!)
	;     5-форм.ст. по центру с разделителем     { 9:30■   20■[  St1]■[St2  ]■R}
	;     6-форм.ст. по центру с [ - ]            { 9:30■   20■[  St1] - [St2  ]■R}
	;     7-без форматирования с разделителем      {9:30■20■St1■St2■R}
	;       (для замены tSPVR)	
	;     8-без форматирования с разделителем в кодах {9:30■20■StCode1■StCode2■R}
	N v,r,R,St1,St2,l,zp,tRE
	Q:VR="" ""
	S VR=$$nVR(VR)
	set DB=$g(DB)
	I '$D(^|DB|tRE(VR)) Q ""
	S zp=$g(%),%="■"
	S:$G(raz)="" raz=%
	S:$G(p)=""!(p>8) p=3
	S tRE=$G(^|DB|tRE(VR)) I tRE="" Q "" 
	S l=12 ;длина станции
	S v=$$VRv(VR)\60,v=v\60_":"_(v#60\10)_(v#10)
	S r=$$VRr(VR)
	S St2=$p(tRE,%,2),St1=$S($p(tRE,%,3)'="":$p(tRE,%,3),1:$p(tRE,%,1))
	I p'=8 S St1=$E(^|DB|tST(St1),1,l),St2=$E(^|DB|tST(St2),1,l)
	S R=$p(tRE,%,4)
	I p'=8 S R=$S($E(R)=2:$E(R,2,8),1:$E(^|DB|tREG($E(R)),1,7))
	S R=R_$J("",7-$L(R))
	S %=zp
	;
	;_______ простой вывод ______________________________________________________
	I p=1 Q v_raz_r_raz_St1_"-"_St2_raz_R
	;_______ форматирование станций по левому краю ______________________________
	I p=2 Q $J(v,5)_raz_$J(r,5)_raz_St1_$J("",l-$L(St1))_"-"_St2_$J("",l-$L(St2))_raz_R
	;_______ форматирование станций по центру ___________________________________
	I p=3 Q $J(v,5)_raz_$J(r,5)_raz_$J(St1,l)_"-"_St2_$J("",l-$L(St2))_raz_R
	;_______ форматирование станций по левому краю с разделителем _______________
	;   (с дополнительным пробелом слева !)
	I p=4 Q $J(v,5)_raz_$J(r,5)_raz_" "_St1_$J("",l-$L(St1))_raz_" "_St2_$J("",l-$L(St2))_raz_R
	;_______ форматирование станций по центру с разделителем ____________________
	I p=5 Q $J(v,5)_raz_$J(r,5)_raz_$J(St1,l)_raz_St2_$J("",l-$L(St2))_raz_R
	;_______ форматирование станций по центру с пробелами [ - ] _________________
	I p=6 Q $J(v,5)_raz_$J(r,5)_raz_$J(St1,l)_" - "_St2_$J("",l-$L(St2))_raz_R
	;_______ простой вывод с разделителями ______________________________________
	I p=7 Q v_raz_r_raz_St1_raz_St2_raz_R
	;_______ простой вывод с разделителями и в кодах_____________________________
	I p=8 Q v_raz_r_raz_St1_raz_St2_raz_R
	;____________________________________________________________________________
	Q ""
	;
	;__преобразование в реальный рейс (без секунд от дообилечиваний)
nVR(VR)	
	Q:$l(VR)>10 VR  ;_это харьковский рейс
	n o,v
	set o=$S($e(VR)?1N:"",1:$e(VR))
	set v=$e(VR,$s(o="":1,1:2),$l(VR)-4)\60*60
	Q o_$s(o="":v,1:$tr($j(v,5)," ","0"))_$e(VR,$l(VR)-3,$l(VR))
	;
	;__проверка на реальный рейс (без секунд от дообилечиваний). 1-реальный
RealVR(VR)
	N t S t=$e(VR,$s($e(VR)?1N:1,1:2),$l(VR)-4) Q (t#60)=0
	;
	;__извлечение времени из полного номера рейса
VRv(VR)	Q +$s($e(VR)'?1N:$e(VR,2,6),$l(VR)>9:$e(VR,1,5),1:$e(VR,1,$L(VR)-4))
	;
	;__извлечение номера рейса из полного номера рейса
VRr(VR)
	I $l(VR)'>10 S r=$S(VR["O":"O"_$E(VR,7,10),1:$E(VR,$L(VR)-3,$L(VR))) S:r?4N r=+r ;_Днепр
	I $l(VR)>10 S r=$tr($e(VR,$l(VR)-5,$l(VR))," ") S:$e(VR)'?1N r=$e(VR)_$j(r,4) S:$l(r)>5 r=$e(r,1,5) ;_Харьков	
	Q r
	;
	;__преобразование полного номера к виду время_raz_рейс
VRS(VR,raz)
	N v,r
	S:$G(raz)="" raz="■"
	S v=$$VRv(VR)\60,v=v\60_":"_(v#60\10)_(v#10)
	S r=$$VRr(VR)
	Q v_raz_r
	;
tUTL	;;Сборник разных общих функций; Квирикадзе В.Р.(с); вер. 1.0; ДОПАС 2006
	quit
	;__Это все появилось после решения расширить количество подсистем
	;__при помощи захвата дополнительного знака из кода системы
	;
	;__Для всех функций, понятие 
	;_____"КодСистемы" это часть кода системы + код подсистемы, всего от 0 до 3 знаков
	;_____"КодПользов" это код пользователя ровно 3 знака
	;
	;__Проверка - наша ли подсистема
	;___в Sp сидит запись типа "КодСистемы" (0-3 знака)
OurSys(Sp,DB)
	if $g(Sp)="" quit 0
	set DB=$g(DB)
	set Sp=$tr($j(Sp,3)," ",0)
	quit Sp=^|DB|tTSYS
	;
	;__Проверка - наш ли пользователь (1-наш)
	;___в User сидит запись типа "КодСистемы"_"КодПользов"
OurUser(User,DB)
	new sp
	if $g(User)="" quit 0
	set sp=$tr($j($e(User,1,$l(User)-3),3)," ",0)
	if sp="" quit 0
	set DB=$g(DB)
	quit sp=^|DB|tTSYS
	;
	;__Проверка - наш ли кассир в билете (1-наш)
	;___в User сидит запись типа "КодСистемы"_"КодПользов"_"Последние 5 знаков билета"
OurKas(User,DB)
	new sp
	if $g(User)="" quit 0
	set DB=$g(DB)
	set sp=$tr($j($e(User,1,$l(User)-8),3)," ",0)
	if sp="" quit 0
	quit sp=^|DB|tTSYS
	;
	;__возврат номера системы (короткого) из номера билета (11 символов)
SysKas(N)
	if $g(N)="" quit ""
	quit +$e(N,1,$l(N)-8)
	;
	;__для рейсов с буквами из харькова
extr(r)	;__отрезаем пробелы слева
	quit $$SL^%WT(r)
	;
	;__Формирование "Сквозных" кодов артикулов для кассового аппарата
MarArtic(Name,Group)
	if $$INFO^%WPRN(4)<3 quit ""
	if $tr($g(Name)," ")="" quit ""
	if $g(Group)="" quit ""
	if $d(^tART(Name,Group))#10 quit $$FormatArticul(^tART(Name,Group))
	set ^tART(Name,Group)=$i(^tART("%CNT"))
	quit $$FormatArticul(^tART(Name,Group))
	;
FormatArticul(art)
	set art=$tr($j(art,4)," ",0)
	quit $tr($e(art),"012345","012345")_$e(art,2,$l(art))
	;
	;
	;__Своя станция с привязкой к рабочему месту
SelfST()
	new id set id=$$ID^%WNET
	quit $g(^tTSYS("S",id),$g(^tTSYS("S")))
